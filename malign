#!/usr/bin/perl

use strict;
use warnings 'FATAL' => 'all';
use threads;
use Thread::Queue;
use Getopt::Std;
our ($opt_p, $opt_t, $opt_w,$opt_d, $opt_m, $opt_f);
getopts('p:t:w:d:mf');

#######
# CLI #
#######

my $PERCENT = 90;
my $THREADS = 4;
my $WRAP    = 50;
my $METHYL  = 0;
my $TEMPDIR = '';
my $FORCE   = 0;

die "
usage: $0 [options] <fasta> <fastq>
options:
  -p <int>  percent identity minimum [$PERCENT]
  -t <int>  threads [$THREADS]
  -w <int>  wrap [$WRAP]
  -d <path> create and keep a named working directory for temp files
  -m        methyl mode
              uses an asymmetric scoring matrix with C:T matching
              computes percent identity from A/G only
  -f        force rewrite over named working directory

" unless @ARGV == 2;
my ($FASTA, $FASTQ) = @ARGV;

$PERCENT = $opt_p if $opt_p;
$THREADS = $opt_t if $opt_t;
$WRAP    = $opt_w if $opt_w;
$METHYL  = $opt_m if $opt_m;
$TEMPDIR = $opt_d if $opt_d;
$FORCE   = $opt_f if $opt_f;

#########
# SETUP #
#########

# emboss needle required
my $needle = `which needle`;
die "you must install 'emboss' and have 'needle' in you path\n" unless $needle;

# temporary directory: named or not
my $WDIR;
if ($TEMPDIR) {
	if (-d $TEMPDIR) {
		die "$TEMPDIR already exists, will not overwrite\n" unless $FORCE;
	}
	$WDIR = $TEMPDIR;
} else {
	$WDIR = "temp.$$.methalign";
}
mkdir $WDIR if not -d $TEMPDIR;	

# reference fasta
open(my $fh, $FASTA) or die;
open(my $of, ">$WDIR/ref.fa") or die;
print $of ">r\n";
my $head = <$fh>;
my $REF;
while (<$fh>) {
	print $of $_;
	chomp;
	$REF .= $_;
}
close $of;
close $fh;

# reads fastq - check for duplicates
my %read;
open(my $fq, $FASTQ) or die;
while (<$fq>) {
	my $s = <$fq>;
	my $x = <$fq>;
	my $q = <$fq>;
	chomp $s;
	$read{$s}++;
}
my $dups = 0;
foreach my $s (keys %read) {$dups++ if $read{$s} > 1}
if ($dups) {print STDERR "$dups duplicates found and skipped\n"}

# scoring matrices - embedded here so no external dependancies

my $normal_matrix = "
    A   C   G   T
A   1  -1  -1  -1
C  -1   1  -1   1
G  -1  -1   1  -1
T  -1  -1  -1   1
";

my $methyl_matrix = "
    A   C   G   T
A   1  -1  -1  -1
C  -1   1  -1   1
G  -1  -1   1  -1
T  -1  -1  -1   1
";

open(my $mat, ">$WDIR/mat") or die;
if ($METHYL) {$mat->print($methyl_matrix)}
else         {$mat->print($normal_matrix)}
close $mat;

END {
	system("rm -rf $WDIR") if defined $WDIR and not $TEMPDIR;
}

#######################
# PAIRWISE ALIGNMENTS #
#######################

my $queries = new Thread::Queue; # query sequences
my $results = new Thread::Queue; # results
$queries->enqueue(keys %read);
$queries->end();

my @worker;
for (my $i = 0; $i < $THREADS; $i++) {
	push @worker, threads->create(\&align, $queries, $results)
}
foreach my $worker (@worker) {$worker->join}
$results->end();

my @align;
my $skipped = 0;
while ($results->pending) {
	my $align = $results->dequeue;
	if (100 * $align->{'pct'} < $PERCENT) {
		$skipped++;
		next;
	}
	push @align, $align;
}
print STDERR "$skipped alignments below $PERCENT\% skipped (",
	scalar(@align), " total)\n";

if (@align == 0) {
	print STDERR "no alignments to process\n";
	exit(0);
}

#######################
# REFERNCE ALIGNMENTS #
#######################

my @aseq;
foreach my $align (@align) {
	my $as;
	for (my $i = 0; $i < length($align->{'seq'}); $i++) {
		my $rnt = substr($align->{'ref'}, $i, 1);
		my $snt = substr($align->{'seq'}, $i, 1);
		next if $rnt eq '-';
		$as .= $snt;
	}
	push @aseq, $as;
}

my $wrap = 50;
for (my $i = 0; $i < length($REF); $i += $WRAP) {
	my $r = substr($REF, $i, $WRAP);
	print($i+1, "\t$r\t", $i+length($r), "\n");
	foreach my $s (@aseq) {
		print("\t", substr($s, $i, $WRAP), "\n");
	}
	print "\n";
}

########################
# POSITION INFORMATION #
########################

my @NT = qw(A C G T);
print join("\t", 'POS', 'REF', @NT), "\n";
for (my $i = 0; $i < length($REF); $i++) {
	my %count = ('A' => 0, 'C' => 0, 'G' => 0, 'T' => 0);
	my $r = substr($REF, $i, 1);
	foreach my $s (@aseq) {
		my $snt = substr($s, $i, 1);
		$count{$snt}++;
	}
	print join("\t", $i+1, $r);
	foreach my $nt (@NT) {
		print "\t", $count{$nt};
	}
	print "\n";	
}

#############
# FUNCTIONS #
#############

sub anti {
	my ($seq) = @_;
	$seq =~ tr/ACGT/TGCA/;
	$seq = reverse $seq;
	return $seq;
}

sub needle {
	my ($seq, $strand) = @_;
	
	# run needle
	my $tid = threads->tid;
	my $fasta = "$WDIR/$tid.$strand.fa";
	my $out = "$WDIR/$tid.$strand.fa.out";
	open(my $sf, ">$fasta") or die;
	print $sf ">s\n$seq\n";
	close $sf;
	
	my @param = (
		"-asequence $WDIR/ref.fa -bsequence $fasta",
		"-gapopen 3 -gapextend 1 -datafile $WDIR/mat -endweight",
		"-outfile $out 2>/dev/null"
	);
		
	system("needle @param") == 0 or die;
	
	# read output, capture aligment sequences and score
	open(my $fh, $out) or die;
	my $ra;
	my $sa;
	my $score;
	while (<$fh>) {
		if (/^# Score:\s+(\S+)/) {$score = $1}
		if (/^([rs])\s+\d+\s+(\S+)\s+\d+/) {
			if ($1 eq 'r') {$ra .= $2}
			if ($1 eq 's') {$sa .= $2}
		}
	}

	# compute gapless percent identity
	my $match = 0;
	my $total = 0;
	for (my $i = 0; $i < length($ra); $i++) {
		my $r = substr($ra, $i, 1);
		my $s = substr($sa, $i, 1);
		next if $r eq '-' or $s eq '-';
		if ($METHYL) {
			if ($r eq 'A' or $r eq 'G') {
				if ($r eq $s) {$match++}
				$total++;
			}
		} else {
			if ($r eq $s) {$match++}
			$total++;
		}
	}
	
	return {'ref' => $ra, 'seq' => $sa, 'pct' => $match/$total};
}

sub align {
	my ($q, $results) = @_;
	
	my $tid = threads->tid;	
	my @stuff;
	my $n = 0;
	while ($q->pending) {
		my $plus = $q->dequeue;
		my $anti = anti($plus);
		
		my $a1 = needle($plus, "$n+");
		my $a2 = needle($anti, "$n-");
		my $max = $a1->{pct} > $a2->{pct} ? $a1 : $a2;
		push @stuff, $max;
		$n++;
	}
	$results->enqueue(@stuff);
}
